/*
 *    Copyright 2014. Binh Nguyen
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "com.gradle.plugin-publish" version "0.15.0"
}

version = projectVersion
group = projectGroup

// Define projects
def twoTenProject = project(':gradle-scalastyle-plugin_2.10')
def twoElevenProject = project(':gradle-scalastyle-plugin_2.11')
def twoTwelveProject = project(':gradle-scalastyle-plugin_2.12')

twoTenProject.ext.scalaDist = "2.10"
twoElevenProject.ext.scalaDist = "2.11"
twoTwelveProject.ext.scalaDist = "2.12"


subprojects {
    apply plugin: "groovy"
    apply plugin: "scala"
    apply plugin: "java"
    apply plugin: "java-gradle-plugin"
    apply from: file("${rootDir}/versionInfo.gradle")


    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    version = projectVersion
    group = projectGroup


    versions.scalaDist = scalaDist


    repositories {
      mavenCentral()
      jcenter()
    }


    dependencies {

      implementation "org.scalastyle:scalastyle_${versions.scalaDist}:${versions.scalastyle}"
      api "junit:junit:${versions.junit}"
      api ("org.spockframework:spock-core:${versions.spock}") {
          exclude group:"org.codehaus.groovy"
      }
    }


    sourceSets {
      main {
          resources {
              srcDirs +=files([file("$rootDir/gradle-scalastyle-plugin_source/src/main/resources")])
          }
          scala {
              srcDirs +=files([file("$rootDir/gradle-scalastyle-plugin_source/src/main/scala")])
          }
          groovy {
              srcDirs +=files([file("$rootDir/gradle-scalastyle-plugin_source/src/main/groovy")])
              compileClasspath += files([sourceSets.main.scala.outputDir])
          }
      }
      test {
          groovy {
              srcDirs +=files([file("$rootDir/gradle-scalastyle-plugin_source/src/test/groovy")])
          }
      }
    }


    compileGroovy.dependsOn(compileScala)


}
